#Область ОписаниеПеременных
 
&НаКлиенте
Перем Компонент;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Компонент = РаботаСКомпонентомСканераШтрихкодов.ПодключитьКомпонент();
	Если Компонент <> Неопределено Тогда 
		
		Компонент.StartGetScan();           
	КонецЕсли;
	
	//Элементы.КартинкаЧО01ВвестиШтрихкод.Видимость = (Компонент = Неопределено);
КонецПроцедуры


&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)                 
	
	Если Событие = "BarcodeDecodeData" И ВводДоступен() тогда 
		
		ОбработатьВводШтрихкодаНаКлиенте(Данные);		
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Компонент = Неопределено;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КартинкаЧО01ВвестиШтрихкодНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВводаШтрихкода", ЭтотОбъект);
	Подсказка = "Введите штрихкод";	
	
	ПоказатьВводСтроки(ОписаниеОповещения, "", Подсказка, 32);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы //<ИмяТаблицыФормы>

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиКомандФормы

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОтправитьДокументНаСервере()
	
	Возврат ОбменДанными.ОтправитьПеремещениеТоваров(Объект.Ссылка);	
КонецФункции

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	Объект.ДатаЗакрытия = ТекущаяДата();
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
	Если ОтправитьДокументНаСервере() Тогда
 	
 		Закрыть();
 	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВводШтрихкодаНаКлиенте(Штрихкод)
	
	Если Штрихкод = "9999999999994" Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Закрыть перемещение?';"), Режим, 0);	
	Иначе
		
		НомерСтроки = НайтиСтрокуПоШтрихкодуНаСервере(Штрихкод);
		Если НомерСтроки >= 0 Тогда
			
			ДанныеСтроки = ПолучитьДанныеСтрокиПоЕеНомеру(НомерСтроки);
			
			ДополнительныеПараметры = Новый Структура("НомерСтроки", НомерСтроки);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВводаЧисла", ЭтотОбъект, ДополнительныеПараметры);
			
			Подсказка = "Введите количество";
			ПоказатьВводЧисла(ОписаниеОповещения, ДанныеСтроки.Количество, Подсказка, 15, 3);
			
		Иначе
			ПоказатьПредупреждение(, СтрШаблон("Товар со штрихкодом %1 не найден", Штрихкод), 5, "Внимание");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеСтрокиПоЕеНомеру(НомерСтроки)
	
	Структура = Новый Структура;
	Структура.Вставить("НомерСтроки");
	Структура.Вставить("Штрихкод1");
	Структура.Вставить("Штрихкод2");
	Структура.Вставить("Идентификатор");
	Структура.Вставить("Наименование");
	Структура.Вставить("Артикул");
	Структура.Вставить("Код");
	Структура.Вставить("Количество");
	Структура.Вставить("Коэффициент");
	Структура.Вставить("ЕдиницаИзмерения");
	Структура.Вставить("ЕдиницаИзмеренияМест");	
	
	ТекущаяСтрока = Объект.Товары[НомерСтроки];
	ЗаполнитьЗначенияСвойств(Структура, ТекущаяСтрока);
	
	Возврат Структура;
КонецФункции

&НаСервере
Функция НайтиСтрокуПоШтрихкодуНаСервере(Штрихкод)
	
	НомерСтроки = -1;
	
	Строки = Объект.Товары.НайтиСтроки(Новый Структура("Штрихкод1", Штрихкод));
	Если Строки.Количество() > 0 Тогда
		
		ТекущаяСтрока = Строки[0];
		Возврат ТекущаяСтрока.НомерСтроки; 
	КонецЕсли;
	
	Строки = Объект.Товары.НайтиСтроки(Новый Структура("Штрихкод2", Штрихкод));
	Если Строки.Количество() > 0 Тогда
		
		ТекущаяСтрока = Строки[0];
		Возврат ТекущаяСтрока.НомерСтроки;			
	КонецЕсли;
	
	Возврат НомерСтроки;
КонецФункции

&НаКлиенте
Процедура ПослеВводаШтрихкода(Штрихкод, ДополнительныеПараметры) Экспорт
    Если НЕ Штрихкод = Неопределено Тогда
        
		ОбработатьВводШтрихкодаНаКлиенте(Штрихкод)
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЧисла(Количество, ДополнительныеПараметры) Экспорт
    Если НЕ Количество = Неопределено Тогда
        
		ТекущаяСтрока = Объект.Товары[ДополнительныеПараметры.НомерСтроки - 1];
		ТекущаяСтрока.Количество = Количество;
		
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись)); 
    КонецЕсли;
КонецПроцедуры

#КонецОбласти
